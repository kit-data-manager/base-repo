plugins { 
      id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'io.freefair.lombok' version '8.0.1'
    id 'io.freefair.maven-publish-java' version '8.0.1'
    id 'org.owasp.dependencycheck' version '8.4.2'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'net.researchgate.release' version '3.0.2'
    id 'com.gorylenko.gradle-git-properties' version '2.4.1'
    id 'java'
    id 'jacoco'
}

jar {
    archiveBaseName = 'base-repo'
    // version is defined in file 'gradle.properties'
    archiveVersion = System.getenv('version')
}

repositories {
    mavenLocal() 
    mavenCentral()
}


//configurations {
 //   all*.exclude module : 'spring-boot-starter-logging'
//}

ext {
    set('javersVersion', "7.0.0")
    set('springBootVersion', "3.1.0")
    set('springDocVersion', "2.1.0")
    set('keycloakVersion', "19.0.0")

    // directory for generated code snippets during tests
    snippetsDir = file("build/generated-snippets")
}

println "Running gradle version: $gradle.gradleVersion"
println "Building ${name} version: ${version}"
println "JDK version: ${JavaVersion.current()}"

sourceCompatibility = 1.17
targetCompatibility = 1.17

if (System.getProperty('profile') == 'minimal') {
    println 'Using minimal profile for building ' + project.getName()
    apply from: 'gradle/profile-minimal.gradle'   
} else {
    println 'Using default profile executing all tests for building ' + project.getName()
    apply from: 'gradle/profile-complete.gradle'
}

dependencies {
   // boot starter
    implementation "org.springframework.boot:spring-boot-starter-validation"    
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-data-rest"
    implementation "org.springframework.boot:spring-boot-starter-mail"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation 'org.springframework.data:spring-data-elasticsearch:5.1.2'

    implementation "org.springframework:spring-messaging:6.0.2"
    implementation "org.springframework.security:spring-security-web:6.1.1"
    implementation "org.springframework.security:spring-security-config:6.1.1"
    
    // cloud support
    implementation "org.springframework.cloud:spring-cloud-starter-config:4.0.3"
    implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:4.0.2"
    implementation "org.springframework.cloud:spring-cloud-gateway-mvc:4.0.6"
    implementation 'de.codecentric:spring-boot-admin-starter-client:3.1.7'


    // springdoc
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-common:${springDocVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-api:${springDocVersion}"

    implementation "edu.kit.datamanager:repo-core:1.2.1"
    implementation "edu.kit.datamanager:service-base:1.2.0"

    //implementation "com.github.victools:jsonschema-generator:4.23.0"

    //Keycloak
    // implementation "org.keycloak:keycloak-spring-boot-starter:${keycloakVersion}"
    implementation "com.nimbusds:nimbus-jose-jwt:9.37"
   // implementation "io.jsonwebtoken:jjwt-api:0.11.5"
    //implementation "io.jsonwebtoken:jjwt-impl:0.11.5"
    //implementation "io.jsonwebtoken:jjwt-jackson:0.11.5"

    implementation "org.javers:javers-core:${javersVersion}"
    implementation "com.github.fge:json-patch:1.9"
    implementation "com.bazaarvoice.jolt:jolt-core:0.1.7"    
    implementation "com.bazaarvoice.jolt:json-utils:0.1.8"    

   // implementation "javax.xml.bind:jaxb-api:2.3.1"

    runtimeOnly    "org.apache.httpcomponents:httpclient:4.5.14"

     // driver for postgres
    implementation "org.postgresql:postgresql:42.6.0"
    //driver for h2
    implementation "com.h2database:h2:2.2.224"

    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc:3.0.0"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework:spring-test"
    testImplementation "org.springframework.security:spring-security-test"
   
    //Java 11 Support 
    testImplementation "org.mockito:mockito-inline:5.2.0"
    testImplementation "junit:junit:4.13.2"
}

if (project.hasProperty('release')) {
    println 'Using \'release\' profile for building ' + project.getName()
    apply from: 'gradle/profile-deploy.gradle'
}

test {
    testLogging {
        outputs.upToDateWhen {false}
        showStandardStreams = true
    }
   environment "spring.config.location", "classpath:/test-config/"
}

tasks.withType(Test) {
    testLogging {
        events 'started', 'passed'
    }
}

springBoot {    
    buildInfo()
}

bootJar {
    println 'Create bootable jar...'
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
    launchScript()
}

// task for printing project name.
task printProjectName {
    doLast {
        println "${project.name}"
    }
}
