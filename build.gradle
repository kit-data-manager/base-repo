plugins { 
    id "org.springframework.boot" version "2.7.2"
    id "io.spring.dependency-management" version "1.0.13.RELEASE" 
    id "io.freefair.lombok" version "6.5.0.3"
    id "io.freefair.maven-publish-java" version "6.5.0.3"
    id "com.github.kt3k.coveralls" version "2.12.0" 
    id "org.owasp.dependencycheck" version "7.1.1"
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id "net.researchgate.release" version "3.0.0"
    id 'java'
    id 'jacoco'
}

jar {
    archiveBaseName = 'base-repo'
    // version is defined in file 'gradle.properties'
    archiveVersion = System.getenv('version')
}

repositories {
    mavenLocal() 
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
 
configurations {
    all*.exclude module : 'spring-boot-starter-logging'
}

ext {
    set('javersVersion', "6.6.5")
    set('springBootVersion', "2.7.2")
    set('springDocVersion', "1.6.9")
    set('keycloakVersion', "19.0.0")

    // directory for generated code snippets during tests
    snippetsDir = file("build/generated-snippets")
}

println "Running gradle version: $gradle.gradleVersion"
println "Building ${name} version: ${version}"
println "JDK version: ${JavaVersion.current()}"

if (System.getProperty('profile') == 'minimal') {
    println 'Using minimal profile for building ' + project.getName()
    apply from: 'gradle/profile-minimal.gradle'   
} else {
    println 'Using default profile executing all tests for building ' + project.getName()
    apply from: 'gradle/profile-complete.gradle'
}

dependencies {
   // boot starter
    implementation "org.springframework.boot:spring-boot-starter-validation"    
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-data-rest"
    implementation "org.springframework.boot:spring-boot-starter-mail"
    implementation "org.springframework.boot:spring-boot-starter-actuator"

     
    implementation "org.springframework:spring-messaging:5.3.22"
    implementation "org.springframework.security:spring-security-web:5.7.3"
    implementation "org.springframework.security:spring-security-config:5.7.3"
    
    // cloud support
    implementation "org.springframework.cloud:spring-cloud-starter-config:3.1.3"
    implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:3.1.4"

    // springdoc
    implementation "org.springdoc:springdoc-openapi-ui:${springDocVersion}"
    implementation "org.springdoc:springdoc-openapi-data-rest:${springDocVersion}"
    implementation "org.springdoc:springdoc-openapi-webmvc-core:${springDocVersion}"

    implementation "edu.kit.datamanager:repo-core:1.0.3"
    implementation "edu.kit.datamanager:service-base:1.0.4"

    //implementation "com.github.victools:jsonschema-generator:4.23.0"

    //Keycloak
    implementation "org.keycloak:keycloak-spring-boot-starter:${keycloakVersion}"
    implementation "com.nimbusds:nimbus-jose-jwt:9.24.3"
    implementation "io.jsonwebtoken:jjwt-api:0.11.5"
    implementation "io.jsonwebtoken:jjwt-impl:0.11.5"
    implementation "io.jsonwebtoken:jjwt-jackson:0.11.5"

    implementation "org.javers:javers-core:${javersVersion}"
    implementation "com.github.fge:json-patch:1.9"
    implementation "com.bazaarvoice.jolt:jolt-core:0.1.7"    
    implementation "com.bazaarvoice.jolt:json-utils:0.1.7"    

    implementation "javax.xml.bind:jaxb-api:2.3.1"

    runtimeOnly    "org.apache.httpcomponents:httpclient:4.5.13"

     // driver for postgres
    implementation "org.postgresql:postgresql:42.5.0"
    //driver for h2
    implementation "com.h2database:h2:2.1.214"

    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc:2.0.6.RELEASE"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework:spring-test"
    testImplementation "org.springframework.security:spring-security-test"
   
    //Java 11 Support 
    testImplementation "org.mockito:mockito-core:4.7.0"
    testImplementation "org.powermock:powermock-module-junit4:2.0.9"
    testImplementation "org.powermock:powermock-api-mockito2:2.0.9"
    testImplementation "junit:junit:4.13.2"
}

if (project.hasProperty('release')) {
    println 'Using \'release\' profile for building ' + project.getName()
    apply from: 'gradle/profile-deploy.gradle'
}

test {
    testLogging {
        outputs.upToDateWhen {false}
        showStandardStreams = true
    }
   environment "spring.config.location", "classpath:/test-config/"
}

tasks.withType(Test) {
    testLogging {
        events 'started', 'passed'
    }
}

bootJar {
    println 'Create bootable jar...'
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
    launchScript()
}

// task for printing project name.
task printProjectName {
    doLast {
        println "${project.name}"
    }
}